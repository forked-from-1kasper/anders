{- The modality of shape infinitesimal in cohesive infinity topos.
   - \Im modality type.

   HoTT 7.7 Modalities

   https://arxiv.org/pdf/1806.05966.pdf

   Copyright (c) Groupoid Infinity, 2014-2022. -}

module infinitesimal where
import library/path
import library/equiv

axiom Im : U -> U
axiom ImUnit (A: U) : A -> Im A
def isCoreduced (A: U): U := isEquiv A (Im A) (ImUnit A)
axiom ImCoreduced (A: U): isCoreduced (Im A)
axiom ImRecursion (A B: U) (c: isCoreduced B) (f: A -> B) : Im A -> B
axiom ImComputeRecursion (A B: U) (c: isCoreduced B) (f: A -> B) (a: A): PathP (<i> B) ((ImRecursion A B c f) (ImUnit A a)) (f a)
def ImApp (A B: U) (f: A -> B) : Im A -> Im B := ImRecursion A (Im B) (ImCoreduced B) (∘ A B (Im B) (ImUnit B) f)
axiom ImNaturality (A B: U) (f: A -> B) : Π (a: A), Path (Im B) ((ImUnit B) (f a)) ((ImApp A B f) (ImUnit A a))
axiom ImInduction (A: U) (B: Im A -> U) (x: Π (a: Im A), isCoreduced (B a)) (y: Π (a: A), B (ImUnit A a)) : Π (a: Im A), B a
axiom ImComputeInduction (A: U) (B: Im A -> U) (c: Π (a: Im A), isCoreduced (B a)) (f: Π (a:A), B (ImUnit A a)) (a: A)
  : Path (B (ImUnit A a)) (f a) ((ImInduction A B c f) (ImUnit A a))

def isInfinitesimalClose (X: U) (a x': X): U := Path (Im X) (ImUnit X a) (ImUnit X x')
def formalDisc (X: U) (a: X): U := Σ (x': X), isInfinitesimalClose X a x'
def unitDisc (X: U) (x: Im X): U := Σ(x': X), Path (Im X) x (ImUnit X x')
def starDisc (X: U) (x: X): formalDisc X x := (x, idp (Im X) (ImUnit X x))
def formalDiscBundle (A: U): U := Σ (a: A), formalDisc A a
axiom differential (X Y: U) (f: X -> Y) (x: X) : formalDisc X x -> formalDisc Y (f x)
axiom preservesInifinitesimalProximity (X Y: U) (x x': X) (f: X -> Y) : isInfinitesimalClose X x x' -> isInfinitesimalClose Y (f x) (f x')

axiom lemma45 (A B C: U) (f: A -> B) (g: B -> C) (x: A)
  : Path ((formalDisc A x -> formalDisc C ((∘ A B C g f) x)))
         (differential A C (∘ A B C g f) x)
         (∘ (formalDisc A x) (formalDisc B (f x)) (formalDisc C ((∘ A B C g f) x)) (differential B C g (f x)) (differential A B f x))
