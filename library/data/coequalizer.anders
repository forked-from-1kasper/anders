{- Coequalizer.

   HoTT 6.8 Pushouts
   HoTT 6.10 Quotients
   HoTT 6.7 Hubs and Spokes
   HoTT 6.9 Truncations

   [1]. Nicolai Kraus, Jakob von Raumer.
   Path spaces of higher inductive types in homotopy type theory.
   http://pure-oai.bham.ac.uk/ws/files/84880431/Path_Spaces.pdf

   [2]. Bauer, Gross, Lumsdaine, Shulman, Sozeau, Spitters [HoTT/Coq]
   The HoTT Library: A formalization of homotopy type theory in Coq
   https://arxiv.org/pdf/1610.04591.pdf

   [3]. Andrej Bauer, Niels van der Weide 
   The Three-HITs Theorem
   https://github.com/nmvdw/Three-HITs

   Copyright © Groupoid Infinity, 2021—2022,
             © @siegment, 2022 -}

module coequalizer where
import library/data/equiv
import library/data/W-type

{- Coequalizer Primitive -}

def coeq′ (A B : U) (f g : A → B) : U := coeq f g
def iota′ (A B : U) (f g : A → B) (x : B) : coeq f g := iota f g x
def resp′ (A B : U) (f g : A → B) (x : A) : Path (coeq f g) (iota f g (f x)) (iota f g (g x)) :=
<i> resp f g x i

def coeq-ind′ (A B : U) (f g : A → B) (X : coeq f g → U) (i : Π (b : B), X (iota f g b))
  (ρ : Π (x : A), PathP (<i> X (resp f g x i)) (i (f x)) (i (g x))) : Π (w : coeq f g), X w :=
coeq-ind X i (λ (x : A) (i : I), ρ x @ i)

def coeq-ind-β₁ (A B : U) (f g : A → B) (X : coeq f g → U) (i : Π (b : B), X (iota f g b))
  (ρ : Π (x : A), PathP (<i> X (resp f g x i)) (i (f x)) (i (g x))) (b : B) :
  Path (X (iota f g b)) (coeq-ind′ A B f g X i ρ (iota f g b)) (i b) :=
<_> i b

def coeq-ind-β₂ (A B : U) (f g : A → B) (X : coeq f g → U) (i : Π (b : B), X (iota f g b))
  (ρ : Π (x : A), PathP (<i> X (resp f g x i)) (i (f x)) (i (g x))) (x : A) :
  Path (PathP (<j> X (resp f g x j)) (i (f x)) (i (g x)))
       (<j> coeq-ind′ A B f g X i ρ (resp f g x j)) (ρ x) :=
<_> ρ x
