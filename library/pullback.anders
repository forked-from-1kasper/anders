{- Homotopy Pullback: https://groupoid.space/math/limits/pullback/

   HoTT 2.15

   Copyright (c) Groupoid Infinity, 2014-2022. -}

module pullback where
import library/path
import library/equiv

--       z2
--    Z ----> B
-- z1 |  pb   | g
--    V       V
--    A ----> C
--        f

def pullback (A B C:U) (f: A -> C) (g: B -> C): U := Σ (a: A) (b: B), Path C (f a) (g b)
def kernel  (A B: U) (f: A -> B): U := pullback A A B f f
def hofiber (A B: U) (f: A -> B) (y: B): U := pullback A unit B f (\(x: unit), y)

def pb1 (A B C: U) (f: A -> C) (g: B -> C): pullback A B C f g -> A := \(x: pullback A B C f g), x.1
def pb2 (A B C: U) (f: A -> C) (g: B -> C): pullback A B C f g -> B := \(x: pullback A B C f g), x.2.1
def pb3 (A B C: U) (f: A -> C) (g: B -> C): Π (x: pullback A B C f g), Path C (f x.1) (g x.2.1) := \(x: pullback A B C f g), x.2.2

def induced (Z A B C: U) (f: A -> C) (g: B -> C) (z1: Z -> A) (z2: Z -> B) (h: Π (z:Z), Path C ((∘ Z A C f z1) z) (((∘ Z B C g z2)) z))
  : Z -> pullback A B C f g
 := \(z: Z), ((z1 z),(z2 z),h z)

def pullbackSq (Z A B C: U) (f: A -> C) (g: B -> C) (z1: Z -> A) (z2: Z -> B): U
 := Σ (h: Π(z:Z), Path C ((∘ Z A C f z1) z) (((∘ Z B C g z2)) z)), isEquiv Z (pullback A B C f g) (induced Z A B C f g z1 z2 h)

def isPullbackSq (Z A B C: U) (f: A -> C) (g: B -> C) (z1: Z -> A) (z2: Z -> B) (h: Π (z:Z), Path C ((∘ Z A C f z1) z) (((∘ Z B C g z2)) z)): U
 := isEquiv Z (pullback A B C f g) (induced Z A B C f g z1 z2 h)

-- Exercise 2.11. Prove that the pullback P :≡ A x_C B defined in (2.15.11) is the corner of a pullback square.
axiom completePullback (A B C: U) (f: A -> C) (g: B -> C) : pullbackSq (pullback A B C f g) A B C f g (pb1 A B C f g) (pb2 A B C f g)

def fiberPullback (A B: U) (f: A -> B) (y: B)
  : pullbackSq (hofiber A B f y) A unit B f (\(x: unit), y) (pb1 A unit B f (\(x: unit), y)) (pb2 A unit B f (\(x: unit), y))
 := completePullback A unit B f (\(x: unit), y)
