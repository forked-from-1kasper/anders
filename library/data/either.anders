module either where

def +ω (n : L) (A B : U n) : U n := Σ (x : 𝟐), ind₂ (λ (_ : 𝟐), U n) A B x
def + (A B : U) : U := +ω L₀ A B

def inl (A B : U) (a : A) : + A B := (0₂, a)
def inr (A B : U) (b : B) : + A B := (1₂, b)

def +-indω (n m : L) (A B : U n) (C : +ω n A B → U m)
  (f : Π (x : A), C (0₂, x)) (g : Π (y : B), C (1₂, y)) (w : +ω n A B) : C w :=
ind₂ (λ (x : 𝟐), Π (y : ind₂ (λ (_ : 𝟐), U n) A B x), C (x, y)) f g w.1 w.2

def +-recω (n m : L) (A B : U n) (C : U m) (f : A → C) (g : B → C) : (+ω n A B) → C :=
+-indω n m A B (λ (w : +ω n A B), C) f g

def +-ind := +-indω L₀ L₀
def +-rec := +-recω L₀ L₀

def +-β₁ (A B : U) (C : + A B → U) (a: A) (f : Π (x : A), C (inl A B x)) (g : Π (y : B), C (inr A B y)) :
  PathP (<_>C (inl A B a)) (f a) (+-ind A B C f g (inl A B a)) :=
<_> f a

def +-β₂ (A B : U) (C : + A B → U) (b: B) (f : Π (x : A), C (inl A B x)) (g : Π (y : B), C (inr A B y)) :
  PathP (<_>C (inr A B b)) (g b) (+-ind A B C f g (inr A B b)) :=
<_> g b

def +-η (A B : U) (c : + A B) :
  + (Σ (x : A), PathP (<_> + A B) c (inl A B x))
    (Σ (y : B), PathP (<_> + A B) c (inr A B y)) :=
+-ind A B (λ (c : + A B),
  + (Σ (x : A), PathP (<_> + A B) c (inl A B x))
    (Σ (y : B), PathP (<_> + A B) c (inr A B y)))
  (λ (x : A), (0₂, (x, <_> (0₂, x))))
  (λ (y : B), (1₂, (y, <_> (1₂, y)))) c
